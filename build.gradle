/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/3.4.1/userguide/tutorial_java_projects.html
 */

// Apply the java plugin to add support for Java
apply plugin: 'java'
apply plugin: 'idea'

// Apply the application plugin to add support for building an application
apply plugin: 'application'
apply plugin: 'maven-publish'
apply plugin: "org.sonarqube"

version = '1.0.0-SNAPSHOT'
group = 'org.ccguyka'


buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.1'
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.3"
    }
}

apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'

sourceCompatibility = 1.8

// In this section you declare where to find the dependencies of your project
repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    // This dependency is found on compile classpath of this component and consumers.
    compile group: 'net.gpedro.integrations.slack', name: 'slack-webhook', version: '1.2.1'
    compile group: 'commons-cli', name: 'commons-cli', version: '1.4'
    compile group: 'com.google.guava', name: 'guava', version: '21.0'
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'

    // Use JUnit test framework
    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:3.6.2'
    testCompile 'nl.jqno.equalsverifier:equalsverifier:2.2.1'
    testCompile "com.github.tomakehurst:wiremock:2.5.1"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

sonarqube {
    properties {
        property "sonar.projectName", "slacker"
        property "sonar.projectKey", "${group}:slacker"
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Gradle Jar File Example',
                'Implementation-Version': version,
                'Main-Class': 'org.ccguyka.slacker.SlackerCli'
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

// Define the main class for the application
mainClassName = 'SlackerCli'

task wrapper(type: Wrapper) {
    gradleVersion = '3.4.1' //version required
}

